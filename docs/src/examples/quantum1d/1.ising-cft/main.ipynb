{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# The Ising CFT spectrum\n",
    "\n",
    "This tutorial is meant to show the finite size CFT spectrum for the quantum Ising model. We\n",
    "do this by first employing an exact diagonalization technique, and then extending the\n",
    "analysis to larger system sizes through the use of MPS techniques."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using MPSKit, MPSKitModels, TensorKit, Plots, KrylovKit\n",
    "using LinearAlgebra: eigvals, diagm, Hermitian"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The hamiltonian is defined on a finite lattice with periodic boundary conditions,\n",
    "which can be implemented as follows:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "L = 12\n",
    "H = periodic_boundary_conditions(transverse_field_ising(), L)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Exact diagonalisation\n",
    "\n",
    "In MPSKit, there is support for exact diagonalisation by leveraging the fact that applying\n",
    "the hamiltonian to an untruncated MPS will result in an effective hamiltonian on the center\n",
    "site which implements the action of the entire hamiltonian. Thus, optimizing the middle\n",
    "tensor is equivalent to optimixing a state in the entire Hilbert space, as all other tensors\n",
    "are just unitary matrices that mix the basis."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "energies, states = exact_diagonalization(H; num=18, alg=Lanczos(; krylovdim=200));\n",
    "plot(real.(energies);\n",
    "     seriestype=:scatter,\n",
    "     legend=false,\n",
    "     ylabel=\"energy\",\n",
    "     xlabel=\"#eigenvalue\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "> **Krylov dimension**\n",
    ">\n",
    "> Note that we have specified a large Krylov dimension as degenerate eigenvalues are\n",
    "> notoriously difficult for iterative methods."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Extracting momentum\n",
    "\n",
    "Given a state, it is possible to assign a momentum label\n",
    "through the use of the translation operator. This operator can be defined in MPO language\n",
    "either diagramatically as\n",
    "\n",
    "![translation operator MPO](translation_mpo.png)\n",
    "\n",
    "or in the code as:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function O_shift(L)\n",
    "    I = id(ComplexF64, ℂ^2)\n",
    "    @tensor O[W S; N E] := I[W; N] * I[S; E]\n",
    "    return periodic_boundary_conditions(InfiniteMPO([O]), L)\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can then calculate the momentum of the groundstate as the expectation value of this\n",
    "operator. However, there is a subtlety because of the degeneracies in the energy\n",
    "eigenvalues. The eigensolver will find an orthonormal basis within each energy subspace, but\n",
    "this basis is not necessarily a basis of eigenstates of the translation operator. In order\n",
    "to fix this, we diagonalize the translation operator within each energy subspace.\n",
    "The resulting energy levels have one-to-one correspondence to the operators in CFT, where\n",
    "the momentum is related to their conformal spin as $P_n = \\frac{2\\pi}{L}S_n$."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function fix_degeneracies(basis)\n",
    "    L = length(basis[1])\n",
    "    M = zeros(ComplexF64, length(basis), length(basis))\n",
    "    T = O_shift(L)\n",
    "    for j in eachindex(basis), i in eachindex(basis)\n",
    "        M[i, j] = dot(basis[i], T, basis[j])\n",
    "    end\n",
    "\n",
    "    vals = eigvals(M)\n",
    "    return angle.(vals)\n",
    "end\n",
    "\n",
    "momenta = Float64[]\n",
    "append!(momenta, fix_degeneracies(states[1:1]))\n",
    "append!(momenta, fix_degeneracies(states[2:2]))\n",
    "append!(momenta, fix_degeneracies(states[3:3]))\n",
    "append!(momenta, fix_degeneracies(states[4:5]))\n",
    "append!(momenta, fix_degeneracies(states[6:9]))\n",
    "append!(momenta, fix_degeneracies(states[10:11]))\n",
    "append!(momenta, fix_degeneracies(states[12:12]))\n",
    "append!(momenta, fix_degeneracies(states[13:16]))\n",
    "append!(momenta, fix_degeneracies(states[17:18]))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can compute the scaling dimensions $\\Delta_n$ of the operators in the CFT from the\n",
    "energy gap of the corresponding excitations as $E_n - E_0 = \\frac{2\\pi v}{L} \\Delta_n$,\n",
    "where $v = 2$. If we plot these scaling dimensions against the momenta, we retrieve the\n",
    "familiar spectrum of the Ising CFT."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "v = 2.0\n",
    "Δ = real.(energies[1:18] .- energies[1]) ./ (2π * v / L)\n",
    "p = plot(momenta, real.(Δ);\n",
    "         seriestype=:scatter, xlabel=\"momentum\", ylabel=\"Δ\", legend=false)\n",
    "vline!(p, [2π / L * i for i in -3:3]; color=\"gray\", linestyle=:dash)\n",
    "hline!(p, [0, 1 / 8, 1, 9 / 8, 2, 17 / 8]; color=\"gray\", linestyle=:dash)\n",
    "p"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Finite bond dimension\n",
    "\n",
    "If we limit the maximum bond dimension of the MPS, we get an approximate solution, but we\n",
    "can reach higher system sizes."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "L_mps = 20\n",
    "H_mps = periodic_boundary_conditions(transverse_field_ising(), L_mps)\n",
    "D = 64\n",
    "ψ, envs, δ = find_groundstate(FiniteMPS(L_mps, ℂ^2, ℂ^D), H_mps, DMRG());"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Excitations on top of the groundstate can be found through the use of the quasiparticle\n",
    "ansatz. This returns quasiparticle states, which can be converted to regular `FiniteMPS`\n",
    "objects."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "E_ex, qps = excitations(H_mps, QuasiparticleAnsatz(), ψ, envs; num=16)\n",
    "states_mps = vcat(ψ, map(qp -> convert(FiniteMPS, qp), qps))\n",
    "energies_mps = map(x -> expectation_value(x, H_mps), states_mps)\n",
    "\n",
    "momenta_mps = Float64[]\n",
    "append!(momenta_mps, fix_degeneracies(states_mps[1:1]))\n",
    "append!(momenta_mps, fix_degeneracies(states_mps[2:2]))\n",
    "append!(momenta_mps, fix_degeneracies(states_mps[3:3]))\n",
    "append!(momenta_mps, fix_degeneracies(states_mps[4:5]))\n",
    "append!(momenta_mps, fix_degeneracies(states_mps[6:9]))\n",
    "append!(momenta_mps, fix_degeneracies(states_mps[10:11]))\n",
    "append!(momenta_mps, fix_degeneracies(states_mps[12:12]))\n",
    "append!(momenta_mps, fix_degeneracies(states_mps[13:16]))\n",
    "\n",
    "v = 2.0\n",
    "Δ = real.(energies_mps[1:18] .- energies_mps[1]) ./ (2π * v / L)\n",
    "plot(momenta_mps, Δ;\n",
    "     seriestype=:scatter, xlabel=\"momentum\", ylabel=\"Δ\", legend=false)\n",
    "vline!(p, [2π / L * i for i in -3:3]; color=\"gray\", linestyle=:dash)\n",
    "hline!(p, [0, 1 / 8, 1, 9 / 8, 2, 17 / 8]; color=\"gray\", linestyle=:dash)\n",
    "p"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  },
  "kernelspec": {
   "name": "julia-1.10",
   "display_name": "Julia 1.10.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}
