{
 "cells": [
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Markdown"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "# Hubbard chain at half filling\n",
    "\n",
    "The Hubbard model is a model of interacting fermions on a lattice, which is often used as a somewhat realistic model for electrons in a solid.\n",
    "The Hamiltonian consists of two terms that describe competing forces of each electron:\n",
    "a kinetic term that allows electrons to hop between neighboring sites, and a potential term reflecting on-site interactions between electrons.\n",
    "Often, a third term is included which serves as a chemical potential to control the number of electrons in the system.\n",
    "\n",
    "$$\n",
    "H = -t \\sum_{\\langle i, j \\rangle, \\sigma} c^{\\dagger}_{i,\\sigma} c_{j,\\sigma} + U \\sum_i n_{i,\\uparrow} n_{i,\\downarrow} - \\mu \\sum_{i,\\sigma} n_{i,\\sigma}\n",
    "$$\n",
    "\n",
    "At half-filling, the system exhibits particle-hole symmetry, which can be made explicit by rewriting the Hamiltonian slightly.\n",
    "First, we fix the overall energy scale by setting `t = 1`, and then shift the total energy by adding a constant `U / 4`, as well as shifting the chemical potential to `N U / 2`.\n",
    "This results in the following Hamiltonian:\n",
    "\n",
    "$$\n",
    "H = - \\sum_{\\langle i, j \\rangle, \\sigma} c^{\\dagger}_{i,\\sigma} c_{j,\\sigma} + U / 4 \\sum_i (1 - 2 n_{i,\\uparrow}) (1 - 2 n_{i,\\downarrow}) - \\mu \\sum_{i,\\sigma} n_{i,\\sigma}\n",
    "$$\n",
    "\n",
    "Finally, setting `\\mu = 0` and defining `u = U / 4` we obtain the Hubbard model at half-filling.\n",
    "\n",
    "$$\n",
    "H = - \\sum_{\\langle i, j \\rangle, \\sigma} c^{\\dagger}_{i,\\sigma} c_{j,\\sigma} + u \\sum_i (1 - 2 n_{i,\\uparrow}) (1 - 2 n_{i,\\downarrow})\n",
    "$$"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using TensorKit\n",
    "using MPSKit\n",
    "using MPSKitModels\n",
    "using SpecialFunctions: besselj0, besselj1\n",
    "using QuadGK: quadgk\n",
    "using Plots\n",
    "using Interpolations\n",
    "using Optim\n",
    "\n",
    "const t = 1.0\n",
    "const mu = 0.0\n",
    "const U = 3.0"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "For this case, the groundstate energy has an analytic solution, which can be used to benchmark the numerical results.\n",
    "It follows from Eq. (6.82) in []().\n",
    "\n",
    "$$\n",
    "e(u) = - u - 4 \\int_0^{\\infty} \\frac{d\\omega}{\\omega} \\frac{J_0(\\omega) J_1(\\omega)}{1 + \\exp(2u \\omega)}\n",
    "$$\n",
    "\n",
    "We can easily verify this by comparing the numerical results to the analytic solution."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function hubbard_energy(u; rtol=1e-12)\n",
    "    integrandum(ω) = besselj0(ω) * besselj1(ω) / (1 + exp(2u * ω)) / ω\n",
    "    int, err = quadgk(integrandum, 0, Inf; rtol=rtol)\n",
    "    return -u - 4 * int\n",
    "end\n",
    "\n",
    "function compute_groundstate(psi, H;\n",
    "                             svalue=1e-3,\n",
    "                             expansionfactor=(1 / 5),\n",
    "                             expansioniter=20)\n",
    "    verbosity = 1\n",
    "    psi, = find_groundstate(psi, H; tol=svalue * 10, verbosity)\n",
    "    for _ in 1:expansioniter\n",
    "        D = maximum(x -> dim(left_virtualspace(psi, x)), 1:length(psi))\n",
    "        D′ = max(2, round(Int, D * expansionfactor))\n",
    "        trscheme = truncbelow(svalue / 10) & truncdim(D′)\n",
    "        psi′, = changebonds(psi, H, OptimalExpand(; trscheme=trscheme))\n",
    "        all(left_virtualspace.(Ref(psi), 1:length(psi)) .==\n",
    "            left_virtualspace.(Ref(psi′), 1:length(psi))) && break\n",
    "        psi, = find_groundstate(psi′, H, VUMPS(; tol=svalue / 5, verbosity))\n",
    "    end\n",
    "\n",
    "    # convergence steps\n",
    "    psi, = changebonds(psi, H, SvdCut(; trscheme=truncbelow(svalue)))\n",
    "    psi, = find_groundstate(psi, H,\n",
    "                            VUMPS(; tol=svalue, verbosity) & GradientGrassmann(; tol=svalue / 100, verbosity))\n",
    "\n",
    "    return psi\n",
    "end\n",
    "\n",
    "H = hubbard_model(InfiniteChain(2); U, t, mu=U / 2)\n",
    "psi = InfiniteMPS(H.data.pspaces, H.data.pspaces)\n",
    "psi = compute_groundstate(psi, H)\n",
    "E = real(expectation_value(psi, H)) / 2\n",
    "@info \"\"\"\n",
    "Groundstate energy:\n",
    "    * numerical: $E\n",
    "    * analytic: $(hubbard_energy(U / 4) - U / 4)\n",
    "\"\"\""
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Symmetries\n",
    "\n",
    "The Hubbard model has a rich symmetry structure, which can be exploited to speed up simulations.\n",
    "Apart from the fermionic parity, the model also has a $U(1)$ particle number symmetry, along with a $SU(2)$ spin symmetry.\n",
    "Explicitly imposing these symmetries on the tensors can greatly reduce the computational cost of the simulation.\n",
    "\n",
    "Naively imposing these symmetries however, is not compatible with our desire to work at half-filling.\n",
    "By construction, imposing symmetries restricts the optimization procedure to a single symmetry sector, which is the trivial sector.\n",
    "In order to work at half-filling, we need to effectively inject one particle per site.\n",
    "In MPSKit, this is achieved by the `add_physical_charge` function, which shifts the physical spaces of the tensors to the desired charge sector."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "H_u1_su2 = hubbard_model(ComplexF64, U1Irrep, SU2Irrep, InfiniteChain(2); U, t, mu=U / 2);\n",
    "charges = fill(FermionParity(1) ⊠ U1Irrep(1) ⊠ SU2Irrep(0), 2);\n",
    "H_u1_su2 = MPSKit.add_physical_charge(H_u1_su2, dual.(charges));\n",
    "\n",
    "pspaces = H_u1_su2.data.pspaces\n",
    "vspaces = [oneunit(eltype(pspaces)), first(pspaces)]\n",
    "psi = InfiniteMPS(pspaces, vspaces)\n",
    "psi = compute_groundstate(psi, H_u1_su2; svalue=1e-3, expansionfactor=1 / 3,\n",
    "                          expansioniter=20)\n",
    "E = real(expectation_value(psi, H_u1_su2)) / 2\n",
    "@info \"\"\"\n",
    "Groundstate energy:\n",
    "    * numerical: $E\n",
    "    * analytic: $(hubbard_energy(U / 4) - U / 4)\n",
    "\"\"\""
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Excitations\n",
    "\n",
    "Because of the integrability, it is known that the Hubbard model has a rich excitation spectrum.\n",
    "The elementary excitations are known as spinons and holons, which are domain walls in the spin and charge sectors, respectively.\n",
    "The fact that the spin and charge sectors are separate is a phenomenon known as spin-charge separation.\n",
    "\n",
    "The domain walls can be constructed by noticing that there are two equivalent groundstates, which differ by a translation over a single site.\n",
    "In other words, the groundstates are $\\psi_{AB}` and $\\psi_{BA}$, where $A$ and $B$ are the two sites.\n",
    "These excitations can be constructed as follows:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "alg = QuasiparticleAnsatz(; tol=1e-3)\n",
    "momenta = range(-π, π; length=33)\n",
    "psi_AB = psi\n",
    "envs_AB = environments(psi_AB, H_u1_su2);\n",
    "psi_BA = circshift(psi, 1)\n",
    "envs_BA = environments(psi_BA, H_u1_su2);\n",
    "\n",
    "spinon_charge = FermionParity(0) ⊠ U1Irrep(0) ⊠ SU2Irrep(1 // 2)\n",
    "E_spinon, ϕ_spinon = excitations(H_u1_su2, alg, momenta,\n",
    "                                 psi_AB, envs_AB, psi_BA, envs_BA;\n",
    "                                 sector=spinon_charge, num=1);\n",
    "\n",
    "holon_charge = FermionParity(1) ⊠ U1Irrep(1) ⊠ SU2Irrep(0)\n",
    "E_holon, ϕ_holon = excitations(H_u1_su2, alg, momenta,\n",
    "                               psi_AB, envs_AB, psi_BA, envs_BA;\n",
    "                               sector=holon_charge, num=1);"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Again, we can compare the numerical results to the analytic solution.\n",
    "Here, the formulae for the excitation energies are expressed in terms of dressed momenta:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function spinon_momentum(Λ, u; rtol=1e-12)\n",
    "    integrandum(ω) = besselj0(ω) * sin(ω * Λ) / ω / cosh(ω * u)\n",
    "    return π / 2 - quadgk(integrandum, 0, Inf; rtol=rtol)[1]\n",
    "end\n",
    "function spinon_energy(Λ, u; rtol=1e-12)\n",
    "    integrandum(ω) = besselj1(ω) * cos(ω * Λ) / ω / cosh(ω * u)\n",
    "    return 2 * quadgk(integrandum, 0, Inf; rtol=rtol)[1]\n",
    "end\n",
    "\n",
    "function holon_momentum(k, u; rtol=1e-12)\n",
    "    integrandum(ω) = besselj0(ω) * sin(ω * sin(k)) / ω / (1 + exp(2u * abs(ω)))\n",
    "    return π / 2 - k - 2 * quadgk(integrandum, 0, Inf; rtol=rtol)[1]\n",
    "end\n",
    "function holon_energy(k, u; rtol=1e-12)\n",
    "    integrandum(ω) = besselj1(ω) * cos(ω * sin(k)) * exp(-ω * u) / ω / cosh(ω * u)\n",
    "    return 2 * cos(k) + 2u + 2 * quadgk(integrandum, 0, Inf; rtol=rtol)[1]\n",
    "end\n",
    "\n",
    "Λs = range(-10, 10; length=51)\n",
    "P_spinon_analytic = rem2pi.(spinon_momentum.(Λs, U / 4), RoundNearest)\n",
    "E_spinon_analytic = spinon_energy.(Λs, U / 4)\n",
    "I_spinon = sortperm(P_spinon_analytic)\n",
    "P_spinon_analytic = P_spinon_analytic[I_spinon]\n",
    "E_spinon_analytic = E_spinon_analytic[I_spinon]\n",
    "P_spinon_analytic = [reverse(-P_spinon_analytic); P_spinon_analytic]\n",
    "E_spinon_analytic = [reverse(E_spinon_analytic); E_spinon_analytic];\n",
    "\n",
    "ks = range(0, 2π; length=51)\n",
    "P_holon_analytic = rem2pi.(holon_momentum.(ks, U / 4), RoundNearest)\n",
    "E_holon_analytic = holon_energy.(ks, U / 4)\n",
    "I_holon = sortperm(P_holon_analytic)\n",
    "P_holon_analytic = P_holon_analytic[I_holon]\n",
    "E_holon_analytic = E_holon_analytic[I_holon];\n",
    "\n",
    "p = let p_excitations = plot(; xaxis=\"momentum\", yaxis=\"energy\")\n",
    "    scatter!(p_excitations, momenta, real(E_spinon); label=\"spinon\")\n",
    "    plot!(p_excitations, P_spinon_analytic, E_spinon_analytic; label=\"spinon (analytic)\")\n",
    "\n",
    "    scatter!(p_excitations, momenta, real(E_holon); label=\"holon\")\n",
    "    plot!(p_excitations, P_holon_analytic, E_holon_analytic; label=\"holon (analytic)\")\n",
    "\n",
    "    p_excitations\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The plot shows some discrepancies between the numerical and analytic results.\n",
    "First and foremost, we must realize that in the thermodynamic limit, the momentum of a domain wall is actually not well-defined.\n",
    "Concretely, only the difference in momentum between the two groundstates is well-defined, as we can always shift the momentum by multiplying one of the groundstates by a phase.\n",
    "Here, we can fix this shift by realizing that our choice of shifting the groundstates by a single site, differs from the formula by a factor $\\pi/2$."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "momenta_shifted = rem2pi.(momenta .- π / 2, RoundNearest)\n",
    "p = let p_excitations = plot(; xaxis=\"momentum\", yaxis=\"energy\", xlims=(-π, π))\n",
    "    scatter!(p_excitations, momenta_shifted, real(E_spinon); label=\"spinon\")\n",
    "    plot!(p_excitations, P_spinon_analytic, E_spinon_analytic; label=\"spinon (analytic)\")\n",
    "\n",
    "    scatter!(p_excitations, momenta_shifted, real(E_holon); label=\"holon\")\n",
    "    plot!(p_excitations, P_holon_analytic, E_holon_analytic; label=\"holon (analytic)\")\n",
    "\n",
    "    p_excitations\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The second discrepancy is that while the spinon dispersion is well-reproduced, the holon dispersion is not.\n",
    "This is due to the fact that the excitation ansatz captures the lowest-energy excitation, and not the elementary single-particle excitation.\n",
    "To make this explicit, we can consider the scattering states comprising of a holon and two spinons.\n",
    "If these are truly scattering states, the energy of the scattering state should be the sum of the energies of the individual excitations, and the momentum is the sum of the momenta.\n",
    "Thus, we can find the lowest-energy scattering states by minimizing the energy over the combination of momenta for the constituent elementary excitations."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "holon_dispersion_itp = linear_interpolation(P_holon_analytic, E_holon_analytic;\n",
    "                                            extrapolation_bc=Line())\n",
    "spinon_dispersion_itp = linear_interpolation(P_spinon_analytic, E_spinon_analytic;\n",
    "                                             extrapolation_bc=Line())\n",
    "function scattering_energy(p1, p2, p3)\n",
    "    p1, p2, p3 = rem2pi.((p1, p2, p3), RoundNearest)\n",
    "    return holon_dispersion_itp(p1) + spinon_dispersion_itp(p2) + spinon_dispersion_itp(p3)\n",
    "end;\n",
    "\n",
    "E_scattering_min = map(momenta_shifted) do p\n",
    "    e = Inf\n",
    "    for i in 1:10 # repeat for stability\n",
    "        res = optimize((rand(2) .* (2π) .- π)) do (p₁, p₂)\n",
    "            p₃ = p - p₁ - p₂\n",
    "            return scattering_energy(p₁, p₂, p₃)\n",
    "        end\n",
    "\n",
    "        e = min(Optim.minimum(res), e)\n",
    "    end\n",
    "    return e\n",
    "end\n",
    "E_scattering_max = map(momenta_shifted) do p\n",
    "    e = -Inf\n",
    "    for i in 1:10 # repeat for stability\n",
    "        res = optimize((rand(Float64, 2) .* (2π) .- π)) do (p₁, p₂)\n",
    "            p₃ = p - p₁ - p₂\n",
    "            return -scattering_energy(p₁, p₂, p₃)\n",
    "        end\n",
    "\n",
    "        e = max(-Optim.minimum(res), e)\n",
    "    end\n",
    "    return e\n",
    "end;\n",
    "\n",
    "p = let p_excitations = plot(; xaxis=\"momentum\", yaxis=\"energy\", xlims=(-π, π),\n",
    "                             ylims=(-0.1, 5))\n",
    "    scatter!(p_excitations, momenta_shifted, real(E_spinon); label=\"spinon\")\n",
    "    plot!(p_excitations, P_spinon_analytic, E_spinon_analytic; label=\"spinon (analytic)\")\n",
    "\n",
    "    scatter!(p_excitations, momenta_shifted, real(E_holon); label=\"holon\")\n",
    "    plot!(p_excitations, P_holon_analytic, E_holon_analytic; label=\"holon (analytic)\")\n",
    "\n",
    "    I = sortperm(momenta_shifted)\n",
    "    plot!(p_excitations, momenta_shifted[I], E_scattering_min[I]; label=\"scattering states\",\n",
    "          fillrange=E_scattering_max[I], fillalpha=0.3, fillstyle=:x)\n",
    "\n",
    "    p_excitations\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.1"
  },
  "kernelspec": {
   "name": "julia-1.11",
   "display_name": "Julia 1.11.1",
   "language": "julia"
  }
 },
 "nbformat": 4
}
